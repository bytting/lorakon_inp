<?xml version="1.0"?>
<component>

<?component error="true" debug="true"?>

<registration
	description="Lorakon Run"
	progid="lorakon-run.WSC"
	version="1.00"
	classid="{c63c0187-311d-423b-bf86-ae4a6f955522}"
>
</registration>

<public>
	<method name="Execute">
		<PARAMETER name="Dsc"/>
		<PARAMETER name="Phase"/>
	</method>

	<method name="Setup">
		<PARAMETER name="Dsc"/>
		<PARAMETER name="Phase"/>
	</method>
</public>

<script language="VBScript" src="campdef.vbs"></script>
<script language="VBScript">
<![CDATA[

function Execute(Dsc,Phase)	

	set csh = CreateObject("WScript.Shell")
	set fso = CreateObject("Scripting.FileSystemObject")

	geniePath = csh.RegRead("HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Canberra Industries, Inc.\Genie-2000 Environment\GENIE2K")
	if not fso.FolderExists(geniePath & "Lorakon") then
		fso.CreateFolder(geniePath & "Lorakon")
	end if
  if not fso.FolderExists(geniePath & "Lorakon\System") then
		fso.CreateFolder(geniePath & "Lorakon\System")
	end if
  
	paramFile = geniePath & "Lorakon\System\input-params.txt"

	set f = fso.OpenTextFile(paramFile, 2, True)
  f.WriteLine Dsc.Parameter(CAM_T_SSPRSTR1) ' Lab
	f.WriteLine Dsc.Parameter(CAM_T_SCOLLNAME) ' Prøvetaker
	f.WriteLine Dsc.Parameter(CAM_T_STITLE) ' Prøvetittel
  f.WriteLine Dsc.Parameter(CAM_T_SDESC1)	' Prøvetype
	f.WriteLine Dsc.Parameter(CAM_T_SIDENT) ' PrøveID
	f.WriteLine Dsc.Parameter(CAM_T_SDESC2) ' Fylke/Kommune
  f.WriteLine Dsc.Parameter(CAM_G_SGPSLATITUDE) ' Breddegrad
  f.WriteLine Dsc.Parameter(CAM_G_SGPSLONGITUDE) ' Lengdegrad
  f.WriteLine Dsc.Parameter(CAM_G_SGPSALTITUDE) ' MOH
	f.WriteLine Dsc.Parameter(CAM_L_SSPI2) ' Lokasjonstype	
	f.WriteLine Dsc.Parameter(CAM_T_SLOCTN) ' Lokasjon
	f.WriteLine Dsc.Parameter(CAM_F_SQUANT) ' Prøvemengde
	f.WriteLine Dsc.Parameter(CAM_F_SQUANTERR) ' Prøvemengde error
	f.WriteLine Dsc.Parameter(CAM_T_SUNITS) ' Prøvemengde enhet
	f.WriteLine Dsc.Parameter(CAM_T_SGEOMTRY) ' Geometri
  f.WriteLine Dsc.Parameter(CAM_X_STIME) ' Referansedato
	f.WriteLine Dsc.Parameter(CAM_F_SSYSERR) ' Syserr
	f.WriteLine Dsc.Parameter(CAM_F_SSYSTERR) ' Systerr
  f.WriteLine Dsc.Parameter(CAM_F_ASP1) ' Start channel
  f.WriteLine Dsc.Parameter(CAM_F_ASP2) ' End channel
  f.WriteLine Dsc.Parameter(CAM_T_SSPBSTR1) ' File ID
	' f.WriteLine Dsc.Parameter(CAM_T_STYPE)
	f.Close	

	csh.Run geniePath & "exefiles\lorakon_inp.exe", 1, true		
	
	set f = fso.OpenTextFile(paramFile, 1)
	idx = 0
	do until f.AtEndOfStream
		line = f.ReadLine		
		select case idx
      case 0 Dsc.Parameter(CAM_T_SSPRSTR1) = line ' Lab
			case 1 Dsc.Parameter(CAM_T_SCOLLNAME) = line ' Prøvetaker
			case 2 Dsc.Parameter(CAM_T_STITLE) = line ' Prøvetittel
      case 3 Dsc.Parameter(CAM_T_SDESC1) = line ' Prøvetype
			case 4 Dsc.Parameter(CAM_T_SIDENT) = line ' PrøveID
			case 5 Dsc.Parameter(CAM_T_SDESC2) = line ' Fylke/Kommune			
      case 6 Dsc.Parameter(CAM_G_SGPSLATITUDE) = CDbl(line) ' Breddegrad
      case 7 Dsc.Parameter(CAM_G_SGPSLONGITUDE) = CDbl(line) ' Lengdegrad
      case 8 Dsc.Parameter(CAM_G_SGPSALTITUDE) = CDbl(line) ' MOH
	    case 9 Dsc.Parameter(CAM_L_SSPI2) = CInt(line) ' Lokasjonstype	
			case 10 Dsc.Parameter(CAM_T_SLOCTN) = line ' Lokasjon
			case 11 Dsc.Parameter(CAM_F_SQUANT) = CDbl(line) ' Prøvemengde
			case 12 Dsc.Parameter(CAM_F_SQUANTERR) = CDbl(line) ' Prøvemengde error
			case 13 Dsc.Parameter(CAM_T_SUNITS) = line ' Prøvemengde enhet
			case 14 Dsc.Parameter(CAM_T_SGEOMTRY) = line ' Geometri
      case 15 Dsc.Parameter(CAM_X_STIME) = CDate(line) ' Referansedato
			case 16
				if len(line) > 0 then
					Dsc.Parameter(CAM_F_SSYSERR) = CDbl(line) ' Syserr
				else
					Dsc.Parameter(CAM_F_SSYSERR) = 0 ' Syserr
				end if
			case 17
				if len(line) > 0 then
					Dsc.Parameter(CAM_F_SSYSTERR) = CDbl(line) ' Systerr
				else
					Dsc.Parameter(CAM_F_SSYSTERR) = 0 ' Systerr
				end if
      case 18 Dsc.Parameter(CAM_F_ASP1) = CDbl(line) ' Start channel
      case 19 Dsc.Parameter(CAM_F_ASP2) = CDbl(line) ' End channel
      case 20 Dsc.Parameter(CAM_T_SSPBSTR1) = line ' File ID
			' case 21 Dsc.Parameter(CAM_T_STYPE) = line
			case else
				exit do
		end select
		idx = idx + 1
	loop
	f.Close
  Dsc.Parameter(CAM_L_SSPI1) = CInt(412441176) ' Magic number
	set fso = Nothing
	set csh = Nothing	

	Execute = 0

end function

function Setup(Dsc,Phase)
	
	Setup = 0

end function

]]>
</script>

</component>
